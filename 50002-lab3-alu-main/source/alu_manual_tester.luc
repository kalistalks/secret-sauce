module alu_manual_tester#(
    CLK_FREQ ~ 1000 : CLK_FREQ > 0
)(
    input clk,  // clock
    input rst, // reset
    input io_button[5], // 5 buttons on IO Shield
    input io_dip[3][8], // DIP switches on IO Shield
    output led[8], // 8 user controllable LEDs
    output io_led[3][8], // LEDs on IO Shield
    output io_segment[8], // 7-Segment LEDs on IO Shield
    output io_select[4] // Digit Select on IO Shield
) {
    alu alu
    
    enum States {
        IDLE,
        A1, // enter in back 16 bits of a
        A2, // enter in front 16 bits of a
        B1, // enter in back 16 bits of b
        B2 // enter in front 16 bits of b
    }
    
    .clk(clk) {
        // The reset conditioner is used to synchronize the reset signal to the FPGA
        // clock. This ensures the entire FPGA comes out of reset at the same time.
        
        // instantiate 5 edge detectors, one for each button 
        edge_detector io_button_edge[5](#RISE(5x{{1}}), #FALL(5x{{0}}))
        // instantiate 5 conditioners, one for each button
        button_conditioner io_button_cond[5](#CLK_FREQ(5x{{1000}})) 
        
        
        .rst(rst) {
            dff states[$width(States)](#INIT(States.IDLE))
            dff a[32](#INIT(0))
            dff b[32](#INIT(0))
        }
    }
    
    
    always {
        // condition the buttons, then take rising edges only
        io_button_cond.in = io_button
        io_button_edge.in = io_button_cond.out
        
        // connect state dff
        states.d = states.q
        a.d = a.q
        b.d = b.q
        
        // alu connection, initialise a and b
        alu.a = a.q
        alu.b = b.q
        
        // leftmost section of IO dip switches, from right to left
        alu.alufn = io_dip[2][5:0]
        
        // leds above dip switches
        io_led = 3x{{8h0}}
        
        // 7 segment display value
        io_segment = 0
        
        // 4 sections of 7 segment leds
        io_select = 4hF
        
        // implement alu tester logic here
        led = alu.out[7:0] // first 8 bits
        io_led[0] = alu.out[15:8] //next 8 bits
        io_led[1] = alu.out[23:16] //next 8 bits
        io_led[2] = alu.out[31:24] // final 8 bits
        
        case (states.q){
            States.IDLE:
                // this takes precedence, placed as the first clause
                if (io_button_edge.out[0]) {
                    states.d = States.A1
                } else if(io_button_edge.out[2]) {
                    states.d = States.B1
                }
            States.A1:
                a.d[7:0] = io_dip[0] // Read back 8 bits
                a.d[15:8] = io_dip[1] // Read front 8 bits
                if (io_button_edge.out[1]) {
                    states.d = States.A2 // Move to States.A2
                }
            States.A2:
                a.d[23:16] = io_dip[0] // Read back 8 bits
                a.d[31:24] = io_dip[1] // Read front 8 bits
                if (io_button_edge.out[1]) {
                    states.d = States.IDLE  // Move to States.IDLE
                }
            States.B1:
                b.d[7:0] = io_dip[0] // Read back 8 bits
                b.d[15:8] = io_dip[1] // Read front 8 bits
                if (io_button_edge.out[1]) {
                    states.d = States.B2  // Move to States.B2
                }
            States.B2:
                b.d[23:16] = io_dip[0] // Read back 8 bits
                b.d[31:24] = io_dip[1] // Read front 8 bits
                if (io_button_edge.out[1]) {
                    states.d = States.IDLE  // Move to States.END
                }
        }
    }
}