module alu_auto_tester#(
    CLK_FREQ ~ 100000000 : CLK_FREQ > 0
)(
    input clk,  // clock
    input rst, // reset
    input io_button[5], // 5 buttons on IO Shield
    input io_dip[3][8], // DIP switches on IO Shield
    output led[8], // 8 user controllable LEDs
    output io_led[3][8], // LEDs on IO Shield
    output io_segment[8], // 7-Segment LEDs on IO Shield
    output io_select[4] // Digit Select on IO Shield
) {
    alu alu
    test_cases test_cases
    sig slow_clock
    bin_to_dec pass_renderer(#DIGITS(4), #LEADING_ZEROS(1))
    bin_to_dec error_renderer(#DIGITS(4), #LEADING_ZEROS(1))
    bin_to_dec test_sel_renderer(#DIGITS(4), #LEADING_ZEROS(1))
    const SIZE = 32
    const CASES = 42
    const SEVEN_SEG_DIV = $is_sim() ? 3 : 16 // put 6 for sim, 16 on hardware
    const SLOW_CLOCK_DEFAULT_SPEED = $is_sim() ? 8 : 25 // put 8  for sim, 25 for hardware
    enum States {
        IDLE,
        RUN,
        // FASTER,
        // SLOWER,
        TESTSEL,
        SHOWA, // show stored value of A
        SHOWB, // show stored value of B
        SHOWALUFN, // show stored value of ALUFN
        CALC, // display output of ALU
        RESULT, // display pass/fail counter
        STOP
    }
    .clk(clk) {
        // The reset conditioner is used to synchronize the reset signal to the FPGA
        // clock. This ensures the entire FPGA comes out of reset at the same time.
        
        // instantiate 5 edge detectors, one for each button 
        edge_detector io_button_edge[5](#RISE(5x{{1}}), #FALL(5x{{0}}))
        // instantiate 1 edge detector for slow clock
        edge_detector slow_clock_edge(#RISE(1), #FALL(0))
        // instantiate 5 conditioners, one for each button
        button_conditioner io_button_cond[5](#CLK_FREQ(5x{{100000000}})) 
        
        .rst(rst) {
            multi_seven_seg seg(#DIV(SEVEN_SEG_DIV))
            dff states[$width(States)](#INIT(States.IDLE))
            dff state_counter[4](#INIT(1))
            
            dff test_sel[$clog2(CASES)](#INIT(0))
            dff pass_counter[$clog2(CASES)](#INIT(0))
            dff error_counter[$clog2(CASES)](#INIT(0))
            
            dff counter[SIZE]
            dff speed_pointer[$clog2(SIZE)](#INIT(SLOW_CLOCK_DEFAULT_SPEED))
            dff slow_clock_enable(#INIT(0))
            
            dff a[32](#INIT(0))
            dff b[32](#INIT(0))
            dff alufn[6](#INIT(0))
            
            dff aluout[32](#INIT(0))
        }
    }
    
    always {
        // condition the buttons, then take rising edges only
        io_button_cond.in = io_button
        io_button_edge.in = io_button_cond.out
        
        // connect state dff
        states.d = states.q
        state_counter.d = state_counter.q
        
        // test case related dffs
        test_sel.d = test_sel.q
        test_sel_renderer.value = test_sel.q
        pass_counter.d = pass_counter.q
        pass_renderer.value = pass_counter.q
        error_counter.d = error_counter.q
        error_renderer.value = error_counter.q
        
        // internal dffs to store a, b and alufn before passing into alu
        a.d = a.q
        b.d = b.q
        alufn.d = alufn.q
        aluout.d = aluout.q
        
        speed_pointer.d = speed_pointer.q
        counter.d = counter.q + 1 // always increment
        slow_clock_enable.d = slow_clock_enable.q
        
        // alu connection, initialise a and b and alufn and store aluout
        alu.a = a.q
        alu.b = b.q
        alu.alufn = alufn.q
        aluout.d = alu.out
        
        // 7 segment display value
        // seg.values takes in 2D 4x4 array, each 4 bit represents number shown on 7 segment
        // z, v, n, state
        seg.values = c{pass_renderer.digits[1:0], error_renderer.digits[1:0]}
        io_segment = ~seg.seg
        io_select = ~seg.sel
        
        // LEDs set to off
        led = 8h0
        io_led = 3x{{8h0}}
        
        // signal connection
        slow_clock = counter.q[speed_pointer.q] // pointed based on the bit of the counter
        
        // picking test case
        test_cases.test_sel = test_sel.q
        
        // this is a mux deciding whether we stop the clock or set it with certain speed 
        case (slow_clock_enable.q){
            0:
                slow_clock_edge.in = 0 // always don't increment or decrement
            1: 
                slow_clock_edge.in = slow_clock 
            default:
                slow_clock_edge.in = 0
        }
        
        case (states.q){
            States.IDLE:
                // this takes precedence
                // if slow clock rising edge, go to UPDATE state
                led = 8h0
                io_led = 3x{{8h0}}
                seg.values = c{pass_renderer.digits[1:0], error_renderer.digits[1:0]}
                if (slow_clock_edge.out) { // this takes precedence
                    if (test_sel.q >= CASES) {
                        states.d = States.STOP
                    } else {
                        test_sel.d = test_sel.q + 1
                        states.d = States.TESTSEL
                    }
                    // if button[1] is pressed
                } else if(io_button_edge.out[1]) {
                    // if slow clock is currently not enabled, run
                    if (~|slow_clock_enable.q){
                        states.d = States.RUN
                    } else { // else, slow clock is currently enabled, stop
                        states.d = States.STOP
                    }
                    //if button 0 is pressed down
                } /* else if(io_button_edge.out[0]) {
                    states.d = States.FASTER
                    //if button 2 is pressed down
                } else if(io_button_edge.out[2]) {
                    states.d = States.SLOWER
                } */            
            
            States.TESTSEL:
                seg.values = test_sel_renderer.digits
                // return to IDLE
                if (slow_clock_edge.out) {
                    states.d = States.SHOWALUFN // Move to States.SHOWALUFN
                }
            
            States.RUN:
                // handle slow clock, next cycle set to 1
                slow_clock_enable.d = 1
                // return to IDLE
                states.d = States.IDLE
            
            States.STOP:
                // handle slow clock, next cycle set to 0
                slow_clock_enable.d = 0
                // return to IDLE
                states.d = States.IDLE
            
            /*
            States.SLOWER: 
                // clamp to 31st bit at max using speed pointer
                if (speed_pointer.q < SIZE-1){
                    speed_pointer.d = speed_pointer.q + 1
                }
                
                // return to IDLE
                states.d = States.IDLE
            
            States.FASTER:
                // clamp to 0st bit at min using speed pointer
                if (speed_pointer.q > 0){
                    speed_pointer.d = speed_pointer.q - 1
                }
                // return to IDLE
                states.d = States.IDLE
            */            
            
            States.SHOWALUFN:
                state_counter.d = 1
                seg.values = {4b0, 4b0, 4b0, state_counter.q}
                // Display 6 bits of ALUFN
                alufn.d = test_cases.alufn_test[5:0]
                led = 8h0
                io_led[2] = test_cases.alufn_test
                if (slow_clock_edge.out) {
                    states.d = States.SHOWA  // Move to States.SHOWA
                } else if(io_button_edge.out[1]) {
                    states.d = States.STOP
                }
            
            States.SHOWA:
                state_counter.d = 2
                seg.values = {4b0, 4b0, 4b0, state_counter.q}
                a.d = test_cases.a_test
                // Display 32 bits of a
                led = a.q[7:0]
                io_led = $build(a.q[31:8],3)
                if (slow_clock_edge.out) {
                    states.d = States.SHOWB // Move to States.SHOWB
                } else if(io_button_edge.out[1]) {
                    states.d = States.STOP
                }
            
            States.SHOWB:
                state_counter.d = 3
                seg.values = {4b0, 4b0, 4b0, state_counter.q}
                // Display 32 bits of b
                b.d = test_cases.b_test
                led = b.q[7:0]
                io_led = $build(b.q[31:8],3)
                if (slow_clock_edge.out) {
                    states.d = States.CALC  // Move to States.CALC
                } else if(io_button_edge.out[1]) {
                    states.d = States.STOP
                }
            
            States.CALC:
                state_counter.d = 4
                seg.values = {c{3b0,alu.z}, c{3b0,alu.v}, c{3b0,alu.n}, state_counter.q}
                if (io_dip[2][7]) {
                    aluout.d = aluout.q ^ 1
                }
                led = aluout.q[7:0] // first 8 bits
                io_led = $build(aluout.q[31:8],3)
                if (slow_clock_edge.out) {
                    states.d = States.RESULT  // Move to States.RESULT
                } else if(io_button_edge.out[1]) {
                    states.d = States.STOP
                }
            
            States.RESULT:
                state_counter.d = 5
                seg.values = {c{3b0,test_cases.zvn[2]}, c{3b0,test_cases.zvn[1]}, c{3b0,test_cases.zvn[0]}, state_counter.q}
                if (io_dip[2][7]) {
                    aluout.d = aluout.q ^ 1
                }
                led = test_cases.result[7:0] // first 8 bits
                io_led = $build(test_cases.result[31:8],3) // next 32 bits
                if (slow_clock_edge.out) {
                    if ((aluout.q == test_cases.result) & (c{alu.z,alu.v,alu.n} == test_cases.zvn)) {
                        pass_counter.d = pass_counter.q + 1
                    } else {
                        error_counter.d = error_counter.q + 1
                    }
                    states.d = States.IDLE  // Move to States.IDLE
                } else if(io_button_edge.out[1]) {
                    states.d = States.STOP
                }
        }
    }
}