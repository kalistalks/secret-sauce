module game_datapath#(
    SLOW_CLOCK_DIV ~ 26 : SLOW_CLOCK_DIV > 0,
    FAST_CLOCK_DIV ~ 21 : FAST_CLOCK_DIV > 0
)(
    input p1_button_left,
    input p1_button_right,
    input p1_button_flip,
    input p2_button_left,
    input p2_button_right,
    input p2_button_flip,
    
    input clk,  // clock
    input rst,  // reset
    
    // output to LEDs/7segments
    output p1_score_out[32],
    output p2_score_out[32],
    output timer_out[32],
    output p1_chef_out[32],
    output p2_chef_out[32],
    output lane_1_color_out[32],
    output lane_2_color_out[32],
    output lane_3_color_out[32],
    output lane_4_color_out[32],
    output lane_1_sushi_out[32],
    output lane_2_sushi_out[32],
    output lane_3_sushi_out[32],
    output lane_4_sushi_out[32],
    
    // debug signals
    output debug_general[4]
) {
    
    //temp variables
    sig input_alu_a[32]
    sig input_alu_b[32]
    
    alu game_alu
    
    .clk(clk){
        counter game_timer_clock(#SIZE(1),#DIV(SLOW_CLOCK_DIV), .rst(rst)) 
        
        // edge detector declaration & connection with counters
        edge_detector edge_detector_game_timer(#RISE(1), #FALL(0), .in(game_timer_clock.value))
        
        game_cu game_cu(
            .p1_button_left(p1_button_left),
            .p1_button_right(p1_button_right),
            .p1_button_flip(p1_button_flip),
            .p2_button_left(p2_button_left),
            .p2_button_right(p2_button_right),
            .p2_button_flip(p2_button_flip),
            .rst(rst),
            .decrease_timer(edge_detector_game_timer.out)
            
        )
        
        game_regfiles game_regfiles(
            .we(game_cu.regfile_we), 
            .wa(game_cu.regfile_wa), 
            .ra1(game_cu.regfile_ra1), 
            .ra2(game_cu.regfile_ra2),
            .rst(rst)
        )
    }
    always {
        // bootstrap rd2 output to CU for convenient branching conditions
        game_cu.regfile_rd2 = game_regfiles.rd2
        
        // asel mux
        case(game_cu.asel){
            b00  : input_alu_a = game_regfiles.rd1
            b01  : input_alu_a = d1 // constant 1
            b10  : input_alu_a = d0 // constant 0
            b11  : input_alu_a = d30 // constant 30
            default :
                input_alu_a = 0
        }
        
        // bsel mux
        case(game_cu.bsel){
            b000 : input_alu_b = game_regfiles.rd2
            b001 : input_alu_b = 1 // constant 1
            b010 : input_alu_b = -1 // constant -1
            b011 : input_alu_b = 2 // constant 2
            b100 : input_alu_b = 0 // constant 0
            b101 : input_alu_b = 8 // constant 8
            b110 : input_alu_b = 3 // constant 3
            b111 : input_alu_b = 4 // constant 4
            default:
                input_alu_b = 0
            
        }
        
        // alu
        game_alu.a = input_alu_a
        game_alu.b = input_alu_b
        game_alu.alufn = game_cu.alufn
        
        // wdsel mux
        case (game_cu.alu_out_sel){
            b01 : game_regfiles.data = hFFFF // WIN
            b10 : game_regfiles.data = hF // DRAW
            b11 : game_regfiles.data = 0 // LOSE
            default : 
                game_regfiles.data = game_alu.out
        }
        
        p1_score_out = game_regfiles.p1_score_out
        p2_score_out = game_regfiles.p2_score_out
        timer_out = game_regfiles.timer_out
        p1_chef_out = game_regfiles.p1_chef_out
        p2_chef_out = game_regfiles.p2_chef_out
        lane_1_color_out = game_regfiles.lane_1_color_out
        lane_2_color_out = game_regfiles.lane_2_color_out
        lane_3_color_out = game_regfiles.lane_3_color_out
        lane_4_color_out = game_regfiles.lane_4_color_out
        lane_1_sushi_out = game_regfiles.lane_1_sushi_out
        lane_2_sushi_out = game_regfiles.lane_2_sushi_out
        lane_3_sushi_out = game_regfiles.lane_3_sushi_out
        lane_4_sushi_out = game_regfiles.lane_4_sushi_out
        
        debug_general = game_cu.debug
    }
}